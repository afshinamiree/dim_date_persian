

BEGIN TRY
	DROP TABLE [dbo].[DimDate]
	DROP FUNCTION Fn_ConvertToSolarDate	
END TRY

BEGIN CATCH
	/*No Action*/
END CATCH
SET QUOTED_IDENTIFIER ON
SET ANSI_NULLS ON
GO
-- ALTERd By MZ 1387/06/25

CREATE function  [dbo].[Fn_ConvertToSolarDate]
(@GregorianDate DateTime,
@OutputType int) 
Returns NvarChar(10)
-- OutputType  Values
-- 0 : Complete With Fix 10 Lenght 
--1 : Complete
--2 : Year Section
--3 : Month Section
--4 : Day Section

 AS
Begin
Declare @Result	VarChar(10)
DECLARE @nIndex numeric
DECLARE @mvarYear numeric
DECLARE @mvarMonth numeric
DECLARE @mvarDay numeric
DECLARE @SolarDate nvarchar(10)
DECLARE @mvarstrMonth nvarchar(2)
DECLARE @mvarstrDay nvarchar(2)

--SET @GregorianDate = REPLACE(CONVERT(nvarchar(10),GETDATE(),120),'-','/')
SET @GregorianDate = REPLACE(CONVERT(nvarchar(10),@GregorianDate,120),'-','/')
SET @nIndex = CONVERT(int,@GregorianDate) - 9941
SET @mvarYear = 1306 + FLOOR(@nIndex / 12053) * 33
SET @nIndex = CAST(@nIndex as int) % 12053
SET @mvarYear = @mvarYear + FLOOR(@nIndex / 1461) * 4
SET @nIndex = CAST(@nIndex as int) % 1461
IF @nIndex = 1460 
BEGIN
	SET @mvarYear  = @mvarYear + 3
	SET @mvarMonth  = 12
	SET @mvarDay  = 30
END
ELSE
BEGIN
	SET @mvarYear  = @mvarYear + FLOOR(@nIndex / 365)
	SET @nIndex  = CAST(@nIndex as int) % 365
	IF @nIndex < (6 * 31) 
	BEGIN
		SET @mvarMonth  = FLOOR(@nIndex / 31) + 1
		SET @mvarDay  = CAST(@nIndex as int) % 31 + 1
	END
	ELSE
	BEGIN
		SET @nIndex  = @nIndex - (6 * 31)
		SET @mvarMonth  = 7 + FLOOR(@nIndex / 30)
		SET @mvarDay  = CAST(@nIndex as int) % 30 + 1
	END
END

SET @SolarDate = LTRIM(STR(@mvarYear)) + '/' + LTRIM(STR(@mvarMonth)) + '/' + LTRIM(STR(@mvarDay)) 

IF @OutputType = 0
BEGIN
	IF @mvarMonth < 10
	BEGIN
		SET @mvarstrMonth = RTRIM('0' + LTRIM(STR(@mvarMonth))) 
	END
	ELSE
	BEGIN
		SET @mvarstrMonth =  LTRIM(STR(@mvarMonth))
	END

	IF @mvarDay < 10
	BEGIN
		SET @mvarstrDay = RTRIM('0' + LTRIM(STR(@mvarDay))) 
	END
	ELSE
	BEGIN
		SET @mvarstrDay =  LTRIM(STR(@mvarDay))
	END
	SET @SolarDate = LTRIM(STR(@mvarYear)) + '/' +@mvarstrMonth + '/' + @mvarstrDay
	
	SET @Result = @SolarDate
END 
ELSE
IF @OutputType = 1
BEGIN
	SET @Result = @SolarDate 
END 
ELSE
IF @OutputType = 2
BEGIN
	SET @Result = @mvarYear 
END 
ELSE
IF @OutputType = 3
BEGIN
	SET @Result = @mvarMonth 
END 
ELSE
IF @OutputType = 4
BEGIN
	SET @Result =  @mvarDay 
END 
ELSE
BEGIN
	SET @Result =  @SolarDate
END
Return @Result
End
GO


/**********************************************************************************/

CREATE TABLE	[dbo].[DimDate]
	(	[DateKey] INT primary key, 

		[Date] DATETIME,
		DateValue INT IDENTITY ,
		[FullDateUK] CHAR(10), -- Date in dd-MM-yyyy format
		[FullDateUSA] CHAR(10),-- Date in MM-dd-yyyy format
		[DayOfMonth] VARCHAR(2), -- Field will hold day number of Month
		[DaySuffix] VARCHAR(4), -- Apply suffix as 1st, 2nd ,3rd etc
		[DayName] VARCHAR(9), -- Contains name of the day, Sunday, Monday 
		[DayOfWeekUSA] CHAR(1),-- First Day Sunday=1 and Saturday=7
		[DayOfWeekUK] CHAR(1),-- First Day Monday=1 and Sunday=7
		[DayOfWeekInMonth] VARCHAR(2), --1st Monday or 2nd Monday in Month
		[DayOfWeekInYear] VARCHAR(2),
		[DayOfQuarter] VARCHAR(3),
		[DayOfYear] VARCHAR(3),
		[WeekOfMonth] VARCHAR(1),-- Week Number of Month 
		[WeekOfQuarter] VARCHAR(2), --Week Number of the Quarter
		[WeekOfYear] VARCHAR(2),--Week Number of the Year
		[Month] VARCHAR(2), --Number of the Month 1 to 12
		[MonthName] VARCHAR(9),--January, February etc
		[MonthOfQuarter] VARCHAR(2),-- Month Number belongs to Quarter
		[Quarter] CHAR(1),
		[QuarterName] VARCHAR(9),--First,Second..
		[Year] CHAR(4),-- Year value of Date stored in Row
		[YearName] CHAR(7), --CY 2012,CY 2013
		[MonthYear] CHAR(10), --Jan-2013,Feb-2013
		[MMYYYY] CHAR(6),
		[FirstDayOfMonth] DATE,
		[LastDayOfMonth] DATE,
		[FirstDayOfQuarter] DATE,
		[LastDayOfQuarter] DATE,
		[FirstDayOfYear] DATE,
		[LastDayOfYear] DATE,
		[IsHolidayUSA] BIT,-- Flag 1=National Holiday, 0-No National Holiday
		[IsWeekday] BIT,-- 0=Week End ,1=Week Day
		[HolidayUSA] VARCHAR(50),--Name of Holiday in US
		[IsHolidayUK] BIT Null,-- Flag 1=National Holiday, 0-No National Holiday
		[HolidayUK] VARCHAR(50) NULL, --Name of Holiday in UK

		 
		PersianDateKey INT NULL,
		PersianDateValue NVARCHAR(10) NULL,

		PersianYear INT NULL,
		PersianMonth INT NULL,
		PersianDayOfMonth INT NULL,

		PersianMonthYear NVARCHAR(15) NULL,
		PersianSeason INT NULL,

		PersianDayOfWeek INT NULL,
		PersianDayOfWeekName NVARCHAR(10) NULL,

		PersianSeasonName NVARCHAR(10) NULL,
		PersianMonthName  NVARCHAR(10) NULL,

		PersianMonthOfSeason INT NULL,
		PersianDayOfYear INT NULL,
		PersianWeekOfYear INT NULL,

		PersianWeekRemainedFromYear INT NULL,
		persianDayRemainedFromyear INT NULL,

	)
GO



DECLARE @StartDate DATETIME = '01/01/2009' --Starting value of Date Range
DECLARE @EndDate DATETIME = '01/01/2035' --End Value of Date Range



--Temporary Variables To Hold the Values During Processing of Each Date of Year
DECLARE
	@DayOfWeekInMonth INT,
	@DayOfWeekInYear INT,
	@DayOfQuarter INT,
	@WeekOfMonth INT,
	@CurrentYear INT,
	@CurrentMonth INT,
	@CurrentQuarter INT

/*Table Data type to store the day of week count for the month and year*/
DECLARE @DayOfWeek TABLE (DOW INT, MonthCount INT, QuarterCount INT, YearCount INT)

INSERT INTO @DayOfWeek VALUES (1, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (2, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (3, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (4, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (5, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (6, 0, 0, 0)
INSERT INTO @DayOfWeek VALUES (7, 0, 0, 0)

--Extract and assign various parts of Values from Current Date to Variable

DECLARE @CurrentDate AS DATETIME = @StartDate
SET @CurrentMonth = DATEPART(MM, @CurrentDate)
SET @CurrentYear = DATEPART(YY, @CurrentDate)
SET @CurrentQuarter = DATEPART(QQ, @CurrentDate)

/********************************************************************************************/
--Proceed only if Start Date(Current date ) is less than End date you specified above

WHILE @CurrentDate < @EndDate
BEGIN
 
/*Begin day of week logic*/

         /*Check for Change in Month of the Current date if Month changed then 
          Change variable value*/
	IF @CurrentMonth != DATEPART(MM, @CurrentDate) 
	BEGIN
		UPDATE @DayOfWeek
		SET MonthCount = 0
		SET @CurrentMonth = DATEPART(MM, @CurrentDate)
	END

        /* Check for Change in Quarter of the Current date if Quarter changed then change 
         Variable value*/

	IF @CurrentQuarter != DATEPART(QQ, @CurrentDate)
	BEGIN
		UPDATE @DayOfWeek
		SET QuarterCount = 0
		SET @CurrentQuarter = DATEPART(QQ, @CurrentDate)
	END
       
        /* Check for Change in Year of the Current date if Year changed then change 
         Variable value*/
	

	IF @CurrentYear != DATEPART(YY, @CurrentDate)
	BEGIN
		UPDATE @DayOfWeek
		SET YearCount = 0
		SET @CurrentYear = DATEPART(YY, @CurrentDate)
	END
	
        -- Set values in table data type created above from variables 

	UPDATE @DayOfWeek
	SET 
		MonthCount = MonthCount + 1,
		QuarterCount = QuarterCount + 1,
		YearCount = YearCount + 1
	WHERE DOW = DATEPART(DW, @CurrentDate)

	SELECT
		@DayOfWeekInMonth = MonthCount,
		@DayOfQuarter = QuarterCount,
		@DayOfWeekInYear = YearCount
	FROM @DayOfWeek
	WHERE DOW = DATEPART(DW, @CurrentDate)
	
/*End day of week logic*/


/* Populate Your Dimension Table with values*/
	
	INSERT INTO [dbo].[DimDate]
	        ( [DateKey] ,
	          [Date] ,
	          [FullDateUK] ,
	          [FullDateUSA] ,
	          [DayOfMonth] ,
	          [DaySuffix] ,
	          [DayName] ,
	          [DayOfWeekUSA] ,
	          [DayOfWeekUK] ,
	          [DayOfWeekInMonth] ,
	          [DayOfWeekInYear] ,
	          [DayOfQuarter] ,
	          [DayOfYear] ,
	          [WeekOfMonth] ,
	          [WeekOfQuarter] ,
	          [WeekOfYear] ,
	          [Month] ,
	          [MonthName] ,
	          [MonthOfQuarter] ,
	          [Quarter] ,
	          [QuarterName] ,
	          [Year] ,
	          [YearName] ,
	          [MonthYear] ,
	          [MMYYYY] ,
	          [FirstDayOfMonth] ,
	          [LastDayOfMonth] ,
	          [FirstDayOfQuarter] ,
	          [LastDayOfQuarter] ,
	          [FirstDayOfYear] ,
	          [LastDayOfYear] ,
	          [IsHolidayUSA] ,
	          [IsWeekday] ,
	          [HolidayUSA] ,
	          [IsHolidayUK] ,
	          [HolidayUK]
	        )
	 
	SELECT
		
		CONVERT (char(8),@CurrentDate,112) as DateKey,
		@CurrentDate AS Date,
		CONVERT (char(10),@CurrentDate,103) as FullDateUK,
		CONVERT (char(10),@CurrentDate,101) as FullDateUSA,
		DATEPART(DD, @CurrentDate) AS DayOfMonth,
		--Apply Suffix values like 1st, 2nd 3rd etc..
		CASE 
			WHEN DATEPART(DD,@CurrentDate) IN (11,12,13) 
			THEN CAST(DATEPART(DD,@CurrentDate) AS VARCHAR) + 'th'
			WHEN RIGHT(DATEPART(DD,@CurrentDate),1) = 1 
			THEN CAST(DATEPART(DD,@CurrentDate) AS VARCHAR) + 'st'
			WHEN RIGHT(DATEPART(DD,@CurrentDate),1) = 2 
			THEN CAST(DATEPART(DD,@CurrentDate) AS VARCHAR) + 'nd'
			WHEN RIGHT(DATEPART(DD,@CurrentDate),1) = 3 
			THEN CAST(DATEPART(DD,@CurrentDate) AS VARCHAR) + 'rd'
			ELSE CAST(DATEPART(DD,@CurrentDate) AS VARCHAR) + 'th' 
			END AS DaySuffix,
		
		DATENAME(DW, @CurrentDate) AS DayName,
		DATEPART(DW, @CurrentDate) AS DayOfWeekUSA,

		-- check for day of week as Per US and change it as per UK format 
		CASE DATEPART(DW, @CurrentDate)
			WHEN 1 THEN 7
			WHEN 2 THEN 1
			WHEN 3 THEN 2
			WHEN 4 THEN 3
			WHEN 5 THEN 4
			WHEN 6 THEN 5
			WHEN 7 THEN 6
			END 
			AS DayOfWeekUK,
		
		@DayOfWeekInMonth AS DayOfWeekInMonth,
		@DayOfWeekInYear AS DayOfWeekInYear,
		@DayOfQuarter AS DayOfQuarter,
		DATEPART(DY, @CurrentDate) AS DayOfYear,
		DATEPART(WW, @CurrentDate) + 1 - DATEPART(WW, CONVERT(VARCHAR, 
		DATEPART(MM, @CurrentDate)) + '/1/' + CONVERT(VARCHAR, 
		DATEPART(YY, @CurrentDate))) AS WeekOfMonth,
		(DATEDIFF(DD, DATEADD(QQ, DATEDIFF(QQ, 0, @CurrentDate), 0), 
		@CurrentDate) / 7) + 1 AS WeekOfQuarter,
		DATEPART(WW, @CurrentDate) AS WeekOfYear,
		DATEPART(MM, @CurrentDate) AS Month,
		DATENAME(MM, @CurrentDate) AS MonthName,
		CASE
			WHEN DATEPART(MM, @CurrentDate) IN (1, 4, 7, 10) THEN 1
			WHEN DATEPART(MM, @CurrentDate) IN (2, 5, 8, 11) THEN 2
			WHEN DATEPART(MM, @CurrentDate) IN (3, 6, 9, 12) THEN 3
			END AS MonthOfQuarter,
		DATEPART(QQ, @CurrentDate) AS Quarter,
		CASE DATEPART(QQ, @CurrentDate)
			WHEN 1 THEN 'First'
			WHEN 2 THEN 'Second'
			WHEN 3 THEN 'Third'
			WHEN 4 THEN 'Fourth'
			END AS QuarterName,
		DATEPART(YEAR, @CurrentDate) AS Year,
		'CY ' + CONVERT(VARCHAR, DATEPART(YEAR, @CurrentDate)) AS YearName,
		LEFT(DATENAME(MM, @CurrentDate), 3) + '-' + CONVERT(VARCHAR, 
		DATEPART(YY, @CurrentDate)) AS MonthYear,
		RIGHT('0' + CONVERT(VARCHAR, DATEPART(MM, @CurrentDate)),2) + 
		CONVERT(VARCHAR, DATEPART(YY, @CurrentDate)) AS MMYYYY,
		CONVERT(DATETIME, CONVERT(DATE, DATEADD(DD, - (DATEPART(DD, 
		@CurrentDate) - 1), @CurrentDate))) AS FirstDayOfMonth,
		CONVERT(DATETIME, CONVERT(DATE, DATEADD(DD, - (DATEPART(DD, 
		(DATEADD(MM, 1, @CurrentDate)))), DATEADD(MM, 1, 
		@CurrentDate)))) AS LastDayOfMonth,
		DATEADD(QQ, DATEDIFF(QQ, 0, @CurrentDate), 0) AS FirstDayOfQuarter,
		DATEADD(QQ, DATEDIFF(QQ, -1, @CurrentDate), -1) AS LastDayOfQuarter,
		CONVERT(DATETIME, '01/01/' + CONVERT(VARCHAR, DATEPART(YY, 
		@CurrentDate))) AS FirstDayOfYear,
		CONVERT(DATETIME, '12/31/' + CONVERT(VARCHAR, DATEPART(YY, 
		@CurrentDate))) AS LastDayOfYear,
		NULL AS IsHolidayUSA,
		CASE DATEPART(DW, @CurrentDate)
			WHEN 1 THEN 0
			WHEN 2 THEN 1
			WHEN 3 THEN 1
			WHEN 4 THEN 1
			WHEN 5 THEN 1
			WHEN 6 THEN 1
			WHEN 7 THEN 0
			END AS IsWeekday,
		NULL AS HolidayUSA, Null, Null

	SET @CurrentDate = DATEADD(DD, 1, @CurrentDate)
END

/********************************************************************************************/
 
--Step 3.
--Update Values of Holiday as per UK Government Declaration for National Holiday.

/*Update HOLIDAY fields of UK as per Govt. Declaration of National Holiday*/
	
-- Good Friday  April 18 
	UPDATE [dbo].[DimDate]
		SET HolidayUK = 'Good Friday'
	WHERE [Month] = 4 AND [DayOfMonth]  = 18

-- Easter Monday  April 21 
	UPDATE [dbo].[DimDate]
		SET HolidayUK = 'Easter Monday'
	WHERE [Month] = 4 AND [DayOfMonth]  = 21

-- Early May Bank Holiday   May 5 
   UPDATE [dbo].[DimDate]
		SET HolidayUK = 'Early May Bank Holiday'
	WHERE [Month] = 5 AND [DayOfMonth]  = 5

-- Spring Bank Holiday  May 26 
	UPDATE [dbo].[DimDate]
		SET HolidayUK = 'Spring Bank Holiday'
	WHERE [Month] = 5 AND [DayOfMonth]  = 26

-- Summer Bank Holiday  August 25 
    UPDATE [dbo].[DimDate]
		SET HolidayUK = 'Summer Bank Holiday'
	WHERE [Month] = 8 AND [DayOfMonth]  = 25

-- Boxing Day  December 26  	
    UPDATE [dbo].[DimDate]
		SET HolidayUK = 'Boxing Day'
	WHERE [Month] = 12 AND [DayOfMonth]  = 26	

--CHRISTMAS
	UPDATE [dbo].[DimDate]
		SET HolidayUK = 'Christmas Day'
	WHERE [Month] = 12 AND [DayOfMonth]  = 25

--New Years Day
	UPDATE [dbo].[DimDate]
		SET HolidayUK  = 'New Year''s Day'
	WHERE [Month] = 1 AND [DayOfMonth] = 1

--Update flag for UK Holidays 1= Holiday, 0=No Holiday
	
	UPDATE [dbo].[DimDate]
		SET IsHolidayUK  = CASE WHEN HolidayUK   IS NULL 
		THEN 0 WHEN HolidayUK   IS NOT NULL THEN 1 END
		
 
--Step 4.
--Update Values of Holiday as per USA Govt. Declaration for National Holiday.

/*Update HOLIDAY Field of USA In dimension*/
	
 	/*THANKSGIVING - Fourth THURSDAY in November*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Thanksgiving Day'
	WHERE
		[Month] = 11 
		AND [DayOfWeekUSA] = 'Thursday' 
		AND DayOfWeekInMonth = 4

	/*CHRISTMAS*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Christmas Day'
		
	WHERE [Month] = 12 AND [DayOfMonth]  = 25

	/*4th of July*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Independance Day'
	WHERE [Month] = 7 AND [DayOfMonth] = 4

	/*New Years Day*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'New Year''s Day'
	WHERE [Month] = 1 AND [DayOfMonth] = 1

	/*Memorial Day - Last Monday in May*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Memorial Day'
	FROM [dbo].[DimDate]
	WHERE DateKey IN 
		(
		SELECT
			MAX(DateKey)
		FROM [dbo].[DimDate]
		WHERE
			[MonthName] = 'May'
			AND [DayOfWeekUSA]  = 'Monday'
		GROUP BY
			[Year],
			[Month]
		)

	/*Labor Day - First Monday in September*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Labor Day'
	FROM [dbo].[DimDate]
	WHERE DateKey IN 
		(
		SELECT
			MIN(DateKey)
		FROM [dbo].[DimDate]
		WHERE
			[MonthName] = 'September'
			AND [DayOfWeekUSA] = 'Monday'
		GROUP BY
			[Year],
			[Month]
		)

	/*Valentine's Day*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Valentine''s Day'
	WHERE
		[Month] = 2 
		AND [DayOfMonth] = 14

	/*Saint Patrick's Day*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Saint Patrick''s Day'
	WHERE
		[Month] = 3
		AND [DayOfMonth] = 17

	/*Martin Luthor King Day - Third Monday in January starting in 1983*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Martin Luthor King Jr Day'
	WHERE
		[Month] = 1
		AND [DayOfWeekUSA]  = 'Monday'
		AND [Year] >= 1983
		AND DayOfWeekInMonth = 3

	/*President's Day - Third Monday in February*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'President''s Day'
	WHERE
		[Month] = 2
		AND [DayOfWeekUSA] = 'Monday'
		AND DayOfWeekInMonth = 3

	/*Mother's Day - Second Sunday of May*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Mother''s Day'
	WHERE
		[Month] = 5
		AND [DayOfWeekUSA] = 'Sunday'
		AND DayOfWeekInMonth = 2

	/*Father's Day - Third Sunday of June*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Father''s Day'
	WHERE
		[Month] = 6
		AND [DayOfWeekUSA] = 'Sunday'
		AND DayOfWeekInMonth = 3

	/*Halloween 10/31*/
	UPDATE [dbo].[DimDate]
		SET HolidayUSA = 'Halloween'
	WHERE
		[Month] = 10
		AND [DayOfMonth] = 31

	/*Election Day - The first Tuesday after the first Monday in November*/
	BEGIN
	DECLARE @Holidays TABLE (ID INT IDENTITY(1,1), 
	DateID int, Week TINYINT, YEAR CHAR(4), DAY CHAR(2))

		INSERT INTO @Holidays(DateID, [Year],[Day])
		SELECT
			DateKey,
			[Year],
			[DayOfMonth] 
		FROM [dbo].[DimDate]
		WHERE
			[Month] = 11
			AND [DayOfWeekUSA] = 'Monday'
		ORDER BY
			YEAR,
			DayOfMonth 

		DECLARE @CNTR INT, @POS INT, @STARTYEAR INT, @ENDYEAR INT, @MINDAY INT

		SELECT
			@CURRENTYEAR = MIN([Year])
			, @STARTYEAR = MIN([Year])
			, @ENDYEAR = MAX([Year])
		FROM @Holidays

		WHILE @CURRENTYEAR <= @ENDYEAR
		BEGIN
			SELECT @CNTR = COUNT([Year])
			FROM @Holidays
			WHERE [Year] = @CURRENTYEAR

			SET @POS = 1

			WHILE @POS <= @CNTR
			BEGIN
				SELECT @MINDAY = MIN(DAY)
				FROM @Holidays
				WHERE
					[Year] = @CURRENTYEAR
					AND [Week] IS NULL

				UPDATE @Holidays
					SET [Week] = @POS
				WHERE
					[Year] = @CURRENTYEAR
					AND [Day] = @MINDAY

				SELECT @POS = @POS + 1
			END

			SELECT @CURRENTYEAR = @CURRENTYEAR + 1
		END

		UPDATE [dbo].[DimDate]
			SET HolidayUSA  = 'Election Day'				
		FROM [dbo].[DimDate] DT
			JOIN @Holidays HL ON (HL.DateID + 1) = DT.DateKey
		WHERE
			[Week] = 1
	END
	--set flag for USA holidays in Dimension
	UPDATE [dbo].[DimDate]
SET IsHolidayUSA = CASE WHEN HolidayUSA  IS NULL THEN 0 WHEN HolidayUSA  IS NOT NULL THEN 1 END,
	PersianDateValue =  dbo.Fn_ConvertToSolarDate(DimDate.Date, 0),
	PersianDateKey = CAST(REPLACE(  dbo.Fn_ConvertToSolarDate(DimDate.Date, 0), '/','') AS INT)
/*****************************************************************************************/
UPDATE DimDate SET 
	PersianYear = CAST(SUBSTRING(DimDate.PersianDateValue , 1,4)  AS INT),
	PersianMonth = CAST(SUBSTRING(DimDate.PersianDateValue , 6,2)  AS INT),
	PersianDayOfMonth = CAST(SUBSTRING(DimDate.PersianDateValue , 9,2)  AS INT) 
 
UPDATE DimDate SET PersianSeason = (PersianMonth-1)/3 + 1,
	 PersianMonthName =   CASE DimDate.PersianMonth 
		WHEN 1 THEN N'فروردین' WHEN 2 THEN N'اردیبهشت' WHEN 3 THEN N'خرداد' 
		WHEN 4 THEN N'تیر' WHEN 5 THEN N'مرداد' WHEN 6 THEN N'شهریور' 
		WHEN 7 THEN N'مهر' WHEN 8 THEN N'آبان' WHEN 9 THEN N'آذر' 
		WHEN 10 THEN N'دی' WHEN 11 THEN N'بهمن' WHEN 12 THEN N'اسفند' END  
	
 UPDATE DimDate SET PersianMonthYear =  CAST(DimDate.PersianYear AS NVARCHAR(4)) + '-'+ DimDate.PersianMonthName,
			  DimDate.PersianSeasonName = CASE PersianSeason WHEN 1 THEN N'بهار' WHEN 2 THEN N'تابستان'
							WHEN 3 THEN N'پاییز' WHEN 4 THEN N'زمستان' END,
			  PersianDayOfWeek = CASE DimDate.DayName WHEN 'Friday' THEN 7 WHEN 'Saturday' THEN 1
								WHEN 'Sunday' THEN 2 WHEN 'Monday' THEN 3 WHEN 'Tuesday' THEN 4 
								WHEN 'Wednesday' THEN 5 WHEN 'Thursday' THEN 6 END ,
			  PersianDayOfWeekName = CASE DimDate.DayName WHEN 'Friday' THEN N'جمعه' WHEN 'Saturday' THEN N'شنبه'
								WHEN 'Sunday' THEN N'یکشنبه' WHEN 'Monday' THEN N'دوشنبه' WHEN 'Tuesday' THEN N'سه شنبه' 
								WHEN 'Wednesday' THEN N'چهارشنبه' WHEN 'Thursday' THEN N'پنج شنبه' END,
			  PersianMonthOfSeason = CASE DimDate.PersianMonth % 3 WHEN 0 THEN 3 ELSE DimDate.PersianMonth % 3 END 
			   
 
 SELECT DD.DateValue ,(SELECT  TOP 1 ISNULL(COUNT(1), 0) +1   FROM DimDate  WHERE  PersianYear = DD.PersianYear AND  PersianDayOfWeek = 1 AND  DimDate.DateValue < DD.DateValue) AS PersianWeekOftheYear,
					 (SELECT  TOP 1 ISNULL(COUNT(1), 0) +1   FROM DimDate  WHERE  PersianYear = DD.PersianYear AND  PersianDayOfWeek = 1  ) AS TotalWeekOftheYear,
					 (SELECT  TOP 1 ISNULL(COUNT(1), 0)     FROM DimDate  WHERE  PersianYear = DD.PersianYear  AND  DimDate.DateValue <= DD.DateValue ) AS  DayOftheYear,
					 (SELECT  TOP 1 ISNULL(COUNT(1), 0)    FROM DimDate  WHERE  PersianYear = DD.PersianYear  ) AS TotalDayOftheYear
INTO #Temp1
FROM DimDate AS DD

UPDATE DD	SET PersianWeekOfYear = T.PersianWeekOftheYear,
				PersianWeekRemainedFromYear = T.TotalWeekOftheYear - T.PersianWeekOftheYear,
				PersianDayOfYear = T.DayOftheYear,
				persianDayRemainedFromyear = T.TotalDayOftheYear - T.DayOftheYear
	FROM DimDate AS DD INNER JOIN #Temp1 AS T ON T.DateValue = DD.DateValue
DROP TABLE #Temp1
		--PersianDayOfYear INT NULL,
		--PersianWeekOfYear INT NULL,

		--PersianWeekRemainedFromYear INT NULL,
		--persianDayRemainedFromyear INT NULL,
DECLARE @FirstYear INT, @LastYear INT
SELECT @FirstYear = MIN(DD.Year), @LastYear = MAX(DD.Year) FROM DimDate AS DD

DELETE DimDate WHERE DimDate.Year IN (@LastYear, @FirstYear)

 SELECT  * FROM DimDate AS DD   

 
